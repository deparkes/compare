plugins {
    id 'java'
}

group 'com.deparkes.compare'
version '0.1'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}



sourceSets {
    integTest {
        java.srcDir file('src/integTest/java')
        resources.srcDir file('src/integTest/resources')
        compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
        runtimeClasspath += output + compileClasspath
    }
}

task integTest(type: Test) {
    description = 'Runs the integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.integTest.output.classesDirs
    classpath = sourceSets.integTest.runtimeClasspath
    mustRunAfter test
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.5.1'
    testImplementation "io.cucumber:cucumber-java:4.7.1"
    testImplementation "io.cucumber:cucumber-junit:4.7.1"
    compile group: 'me.xdrop', name: 'fuzzywuzzy', version: '1.2.0'
}

jar {
    manifest {
        attributes 'Main-Class' : 'Main'

    }
}

test {
    // Enable JUnit 5 (Gradle 4.6+).
    //useJUnitPlatform()

    // Always run tests, even when nothing changed.
    dependsOn 'cleanTest'

    // Show test results.
    testLogging {
        events "passed", "skipped", "failed"
    }
}

integTest {
    // Enable JUnit 5 (Gradle 4.6+).
    //useJUnitPlatform()

    // Always run tests, even when nothing changed.
    dependsOn 'cleanIntegTest'

    // Show test results.
    testLogging {
        events "passed", "skipped", "failed"
    }
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

task cucumber() {
    dependsOn assemble, compileTestJava
    doLast {
        javaexec {
            main = "io.cucumber.core.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = ['--plugin', 'pretty', '--glue', 'gradle.cucumber', 'src/test/resources']
        }
    }
}